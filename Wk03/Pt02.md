# Wk03x02 - Recursive functions on lists

In Haskell, there are two different approaches to working with lists.

* Writing your own functions to do what you want that recurse through the list structure
* Write combinations of functions in the Haskell standard set that do what you want

It's usually preferred to use the second option, however they do still need to be defined. This is done with the first method.

## Recursion on lists

A list is built from the empty list `[]` and the `cons` function in Haskell (`cons :: a -> [a] -> [a]`). The word *cons* is written as the `:` operator and that operator is pronounced as such.

Every list is either:

* `[]`
* `(x:xs)` where `x` is the head of the list and `xs` is the tail of the list

This means the recursive definition looks something like this:

* The base case is `[]`
* The recursive case is `(x:xs)`

### Examples of recursion on lists

#### `length`

```hs
length :: [a] -> Int
length [] = 0
length (x:xs) = 1 + length xs
```

#### `filter`

`filter` is a function that takes a *predicate* (a function that returns a boolean value) and returns a list of elements that satisfy that condition. For example:

```hs
filter (<5) [3,9,2,12,6,4] -- > [3,2,4]
```

The library definiton for filter is as follows. It uses a feature of Haskell called *guards*, which have not yet been covered.

```hs
filter :: (a -> Bool) -> [a] -> [a]
filter pred []    = []
filter pred (x:xs)
  | pred x         = x : filter pred xs
  | otherwise      = filter pred xs
```

## Computations on lists

Many things that would be done with lists would usually involve a `for` or `while` loop in other languages. Haskell doesn't have these, so there are a few other functions that sort of go in the place of those.

* `map` performs a computation on every element of a list
* `foldl` iterates over the list from left to right
* `foldr` iterates over the list from right to left

(this isn't an exhaustive list)

### Function composition

A larger or more complicated computation can be expressed by multiple smaller and chained functions. This is called *function composition*.

1. Start with type `a`.
2. Apply a function `g :: a -> b` 
3. Apply a function to the intermediate result `f :: b -> c`

The entire computation (`gf`) can be written as `f O g` in funky mathematical notation. Haskell uses `.` as the function composition operator.

Just like in regular maths, functions are executed from right to left. 

```hs
(.) :: (b -> c) -> (a -> b) -> a -> c
(f . g) x = f (g x)
```

## `map`

`map` applies a function to every element in a list.

```hs
map f [x0, x1, x2] -- > [f x0, f x1, f x2]
```

`map` is one of the most commonly used functions in functional programming, and a common programming style is to define a simple set of computations using `map` and then compose them into one.

```hs
map f (map g xs) = map (f . g) xs
```

### Definition

```hs
map :: (a -> b) -> [a] -> [b]
map _ [] = []
map f (x:xs) = f x : map f xs
```

## Reducing a list

An iteration over a list to produce a single value is called a *fold*. There are multiple different types, including left folding, right folding, variations to do with *initialisation* and various others too.

### `foldl` - left fold

`foldl` performs a fold going from the left to right through a list. A typical application of `foldl` looks like `foldl f z x s`:

* `z :: b` is the initial value
  * This can also be thought of as the accumulator.
* `xs :: [a]` is a list of values that we will combine using
* `f`, the function used to combine values in the list

Function `f` takes the current value of the accumulator and a list element and returns the new value of the accumulator.

```hs
foldl :: (b -> a -> b) -> b -> [a] -> b
```

In function notation, `foldl f z [x0, x1, x2]` looks like `f(f(f z x0) x1) x2`.

To get the sum of all values in a list, we can use the `+` operator as an infix operator.

```hs
foldl (+) z [] -- > z
foldl (+) z [x0] -- > z + x0
foldl (+) z [x0,x1] -- > (z + x0) + x1
foldl (+) z [x0,x1,x2] -- > ((z + x0) + x1) + x2
```

##### Definition

```hs
foldl        :: (b -> a -> b) -> b -> [a] -> b
foldl f z0 xs0 = lgo z0 xs0
             where
                lgo z []     =  z
                lgo z (x:xs) = lgo (f z x) xs
```

### `foldr` - right fold

`foldr` is pretty much the same as `foldl`, except that it works from right to left.

```hs
foldr :: (a -> b -> b) -> b -> [a] -> b
```

In In function notation, `foldr f z [x0, x1, x2]` looks like `f 0x (f 1x (f x2 z))`.

Again, we can sum all values in a list using the `+` operator.

```hs
foldr (+) z [] -- > z
foldr (+) z [x0] -- > x0 + z
foldr (+) z [x0,x1] -- > x0 + (x1 + z)
foldr (+) z [x0,x1,x2] -- > x0 + (x1 + (x2 + z))
```

#### Definition

```hs
foldr            :: (a -> b -> b) -> b -> [a] -> b
foldr k z = go
          where
            go []     = z
            go (y:ys) = y `k` go ys
```

#### The relationship between `foldr` and list structure

If `[x0, x1, x2]` can be written as `x0:x1:x2:[]`, we can use *cons* and `foldr` on an empty list to achieve the same thing.

```hs
foldr (:) [] [x0, x1, x2] -- > x0:x1:x2:[]
```

Hence we can formalise this relationship as `foldr cons [] xs = xs`.

### Some applications of folds

```hs
sum xs = foldr (+) 0 xs
product xs = foldr (*) 1 xs
```

In each of these cases, we can "factor out" the `xs` that appears on each side, and write

```hs
sum = foldr (+) 0
product = foldr (*) 1
```

This style is sometimes called *point free* since you're programming entirely with functions and you're not mentioning the data directly.

These functions work as you would expect them to.

```hs
sum [1, 2, 3, 4, 5] -- > 15
product [1, 2, 3, 4, 5] -- > 120
```

